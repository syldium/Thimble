plugins {
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

defaultTasks 'build'

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    repositories {
        mavenCentral()
        maven {
            name = 'sonatype-oss-snapshots1'
            url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name = 'sonatype-oss'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url = 'https://libraries.minecraft.net'
            content {
                includeGroup 'com.mojang'
            }
        }
        maven {
            url = 'https://repo.papermc.io/repository/maven-public/'
            content {
                includeGroup 'com.destroystokyo.paper'
                includeGroup 'io.papermc.paper'
                includeGroup 'net.md-5'
            }
        }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
            content {
                includeGroup 'org.spongepowered'
            }
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    project.description = 'Plugin for the thimble mini game (dé à coudre)'
    project.ext.fullVersion = '1.5.4'

    jar {
        manifest {
            attributes 'Implementation-Title': 'Thimble',
                    'Implementation-Version': project.ext.fullVersion
        }
    }

    configurations.all {
        exclude group: "org.ow2.asm", module: "asm"
        exclude group: "org.ow2.asm", module: "asm-commons"
        exclude group: "org.bukkit", module: "bukkit"
        exclude group: "org.checkerframework", module: "checker-qual"
        exclude group: "com.googlecode.json-simple", module: "json-simple"
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.spigotmc", module: "spigot-api"
    }
}