plugins {
    id 'xyz.jpenilla.run-paper' version '1.0.4'
}

apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
}

dependencies {
    implementation project(':api')
    implementation project(':common')
    implementation project(':bukkit:integration')

    compileOnly(libs.paper) {
        exclude(module: 'adventure-api')
    }
    compileOnly(libs.paperBrigadier)
    compileOnly 'it.unimi.dsi:fastutil:8.4.4'
    implementation("net.kyori:adventure-platform-bukkit:${libs.versions.adventurePlatform.get()}") {
        exclude(module: 'checker-qual')
        exclude(module: 'guava')
    }
    implementation 'org.bstats:bstats-bukkit:2.2.1'
}

processResources {
    filesMatching("plugin.yml") {
        expand(
                'pluginVersion': project.ext.fullVersion,
                'description': project.properties.description
        )
    }
}

shadowJar {
    archiveBaseName = "${project.parent.name}-Bukkit"
    archiveClassifier = ''
    archiveVersion = project.ext.fullVersion

    dependencies {
        exclude(dependency('org.jetbrains:annotations'))
    }

    relocate 'net.kyori.adventure', 'me.syldium.thimble.lib.adventure'
    relocate 'net.kyori.examination', 'me.syldium.thimble.lib.examination'
    relocate 'org.bstats', 'me.syldium.thimble.lib.metrics'
    minimize()
}

artifacts {
    archives shadowJar
}

runServer {
    minecraftVersion('1.17.1')
}

tasks.withType(xyz.jpenilla.runpaper.RunPaper).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(16)
    }
}
