plugins {
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

apply plugin: 'com.gradleup.shadow'

repositories {
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

java {
    disableAutoTargetJvm()
}

dependencies {
    implementation project(':api')
    implementation project(':common')
    implementation project(':bukkit:integration')

    compileOnly(libs.paper)
    implementation 'org.bstats:bstats-bukkit:3.1.0'
}

processResources {
    filesMatching("plugin.yml") {
        expand(
                'pluginVersion': project.ext.fullVersion,
                'description': project.properties.description
        )
    }
}

jar {
    manifest {
        attributes 'paperweight-mappings-namespace': 'mojang'
    }
}

shadowJar {
    archiveBaseName = "${project.parent.name}-Paper"
    archiveClassifier = ''
    archiveVersion = project.ext.fullVersion

    dependencies {
        exclude(dependency('org.jetbrains:annotations'))
    }

    relocate 'org.bstats', 'me.syldium.thimble.lib.metrics'
    minimize()
}

artifacts {
    archives shadowJar
}

runServer {
    minecraftVersion('1.21.1')
}
