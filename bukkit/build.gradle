plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://libraries.minecraft.net' }
}

dependencies {
    implementation project(':api')
    implementation project(':common')
    implementation project(':bukkit:integration')

    compileOnly ('com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT') {
        exclude(module: 'json-simple')
    }
    compileOnly 'com.destroystokyo.paper:paper-mojangapi:1.16.4-R0.1-SNAPSHOT'
    compileOnly 'it.unimi.dsi:fastutil:8.4.4'
    implementation ('net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT') {
        exclude(module: 'checker-qual')
        exclude(module: 'guava')
    }
    implementation 'org.bstats:bstats-bukkit:1.8'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': project.ext.fullVersion
        expand(project.properties)
        include 'plugin.yml'
    }
}

shadowJar {
    archiveBaseName = "Thimble-Bukkit-${project.ext.fullVersion}.jar"

    dependencies {
        exclude(dependency('org.checkerframework:checker-qual'))
        exclude(dependency('org.jetbrains:annotations'))
    }

    relocate 'net.kyori.adventure', 'me.syldium.thimble.lib.adventure'
    relocate 'net.kyori.examination', 'me.syldium.thimble.lib.examination'
    relocate 'org.bstats.bukkit', 'me.syldium.thimble.lib.metrics'
    minimize()
}

artifacts {
    archives shadowJar
}
