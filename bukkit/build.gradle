plugins {
    id 'xyz.jpenilla.run-paper' version '2.3.0'
}

apply plugin: 'io.github.goooler.shadow'

repositories {
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

java {
    disableAutoTargetJvm()
}

dependencies {
    implementation project(':api')
    implementation project(':common')
    implementation project(':bukkit:integration')

    compileOnly(libs.paper) {
        exclude(module: 'adventure-api')
    }
    implementation("net.kyori:adventure-platform-bukkit:${libs.versions.adventurePlatform.get()}") {
        exclude(module: 'checker-qual')
        exclude(module: 'guava')
    }
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

processResources {
    filesMatching("plugin.yml") {
        expand(
                'pluginVersion': project.ext.fullVersion,
                'description': project.properties.description
        )
    }
}

jar {
    manifest {
        attributes 'paperweight-mappings-namespace': 'mojang'
    }
}

shadowJar {
    archiveBaseName = "${project.parent.name}-Bukkit"
    archiveClassifier = ''
    archiveVersion = project.ext.fullVersion

    dependencies {
        exclude(dependency('org.jetbrains:annotations'))
    }

    relocate 'net.kyori.adventure', 'me.syldium.thimble.lib.adventure'
    relocate 'net.kyori.examination', 'me.syldium.thimble.lib.examination'
    relocate 'net.kyori.option', 'me.syldium.thimble.lib.option'
    relocate 'org.bstats', 'me.syldium.thimble.lib.metrics'
    minimize()
}

artifacts {
    archives shadowJar
}

runServer {
    minecraftVersion('1.21')
}
